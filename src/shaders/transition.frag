uniform float uProgress;
uniform float uTime;
uniform vec2 uResolution;
uniform vec2 uDirection;

varying vec2 vUv;

float hash(vec2 p) {
    return fract(sin(dot(p, vec2(127.1, 311.7))) * 43758.5453123);
}

float noise(vec2 p) {
    vec2 i = floor(p);
    vec2 f = fract(p);
    f = f * f * (3.0 - 2.0 * f);
    float a = hash(i);
    float b = hash(i + vec2(1.0, 0.0));
    float c = hash(i + vec2(0.0, 1.0));
    float d = hash(i + vec2(1.0, 1.0));
    return mix(mix(a, b, f.x), mix(c, d, f.x), f.y);
}

float fbm(vec2 p) {
    float value = 0.0;
    float amplitude = 0.5;
    for (int i = 0; i < 5; i++) {
        value += amplitude * noise(p);
        p *= 2.0;
        amplitude *= 0.5;
    }
    return value;
}

void main() {
    vec2 uv = vUv;

    float n = fbm(uv * 4.0 + uTime * 0.5);
    float progress = uProgress;

    float edge = 0.1;
    float dissolve = smoothstep(progress - edge, progress + edge, n + (1.0 - uv.y) * 0.3);

    float glow = smoothstep(progress + edge, progress, n + (1.0 - uv.y) * 0.3) *
                 smoothstep(progress - edge * 3.0, progress - edge, n + (1.0 - uv.y) * 0.3);

    vec3 color = vec3(0.0);
    color += vec3(1.0) * glow * 0.5;

    float alpha = 1.0 - dissolve;

    gl_FragColor = vec4(color, alpha);
}
